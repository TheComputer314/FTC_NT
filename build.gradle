/**
 * Top-level build file for ftc_app project.
 *
 * It is extraordinarily rare that you will ever need to edit this file.
 */

buildscript {
    repositories {
        mavenCentral()
        google()
    }
    dependencies {
        // Note for FTC Teams: Do not modify this yourself.
        //noinspection AndroidGradlePluginVersion
        classpath 'com.android.tools.build:gradle:8.6.0'
    }
}

plugins {
    //noinspection JavaPluginLanguageLevel
    id "java"
    id "com.diffplug.spotless" version "6.25.0"
}

// This is now required because aapt2 has to be downloaded from the
// google() repository beginning with version 3.2 of the Android Gradle Plugin
allprojects {
    repositories {
        mavenCentral()
        google()
    }
}

repositories {
    mavenCentral()
}

compileJava.dependsOn(spotlessApply)
spotless {
    java {
        target fileTree(".") {
            include "**/*.java"
            exclude "**/build/**", "**/build-*/**", "FtcRobotController/**"
        }
        removeUnusedImports()
        importOrder()
        cleanthat()
        googleJavaFormat()
        endWithNewline()
        licenseHeader("// Copyright (c) 2024-2025 FTC 8696\n// All rights reserved.\n\n")
    }
    groovyGradle {
        target fileTree(".") {
            include "**/*.gradle"
            exclude "**/build/**", "**/build-*/**"
        }
        greclipse()
        indentWithSpaces(4)
        trimTrailingWhitespace()
        endWithNewline()
    }
    format "misc", {
        target fileTree(".") {
            include "**/*.md", "**/.gitignore"
            exclude "**/build/**", "**/build-*/**", "FtcRobotController/**"
        }
        trimTrailingWhitespace()
        indentWithSpaces(2)
        endWithNewline()
    }
}
